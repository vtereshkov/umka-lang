// Umka standard library

// Error handling

type (
    ErrPos* = struct {
        file: str
        func: str
        line: int
    }

    Err* = struct {
        code: int
        msg: str
        sender: any
        trace: []ErrPos
    }

    StdErr* = enum {
        ok               // No error
        assert           // Assertion failed
        buffer           // Wrong buffer size
        ptr              // Pointers cannot be read or written
        nullf            // File is null
        eof              // End of file
    }    
)

stdErrMsg := []str{
    "",
    "Assertion failed",
    "Wrong buffer size",
    "Pointers cannot be read or written",
    "File is null",
    "End of file"
}

fn rtltrace(depth: int, pos: ^ErrPos): int

fn error*(code: int = 0, msg: str = "", sender: any = null): Err {
    err := Err{code, msg, sender, {}}
    if code != 0 {
        var pos: ErrPos
        for depth := 2; depth < 10 && rtltrace(depth, &pos) == 0; depth++ {
            err.trace = append(err.trace, pos)
        } 
    }
    return err
}

fn stderror(code: StdErr): Err {
    return error(int(code), stdErrMsg[int(code)], "std")
}

fn exitif*(err: Err) {
    if err.code != 0 {
        msg := sprintf("%s (code %d) in %v\n", err.msg, err.code, err.sender)
        msg += sprintf("Stack trace:\n")
        for _, pos in err.trace {
            msg += sprintf("    %s: %s (%d)\n", pos.func, pos.file, pos.line)
        }
        exit(err.code, msg)
    }    
}

fn assert*(cond: bool, msg: str = "") {
    if !cond {
        err := stderror(.assert)
        if msg != "" {
            err.msg += ": " + msg
        }
        exitif(err)
    }
}

// Memory

fn rtlmemcpy(dest, src: ^void, count: int)

fn tobytes*(buf: any): []uint8 {
    if !valid(buf) {
        return {}
    }
    bytes := make([]uint8, sizeofself(buf))
    rtlmemcpy(&bytes[0], buf.__self, sizeofself(buf))
    return bytes
}

fn frombytes*(buf: any, bytes: []uint8): Err {
    if !valid(buf) || sizeofself(buf) != len(bytes) {
        return stderror(.buffer)
    }    
    if selfhasptr(buf) {
        return stderror(.ptr)
    }
    rtlmemcpy(buf.__self, &bytes[0], sizeofself(buf))
    return {}    
}

// Input/output

type (
    File* = ^struct {}

    SeekFrom* = enum {
        begin
        cur
        end
    }
)

fn rtlstdin(): File

fn stdin*(): File {
    return rtlstdin()
}

fn rtlstdout(): File

fn stdout*(): File {
    return rtlstdout()
}

fn rtlstderr(): File

fn stderr*(): File {
    return rtlstderr()
}

fn rtlfopen(name: str, mode: str): File

fn fopen*(name: str, mode: str): (File, Err) {
    f := rtlfopen(name, mode)
    return f, f != null ? stderror(.ok) : stderror(.nullf)
}

fn rtlfclose(f: File): int

fn fclose*(f: File): Err {
    if f == null {
        return stderror(.nullf)
    }    
    return rtlfclose(f) == 0 ? stderror(.ok) : stderror(.eof)
}

fn rtlfseek(f: File, offset: int, origin: SeekFrom): int

fn fseek*(f: File, offset: int, origin: SeekFrom): Err {
    if f == null {
        return stderror(.nullf)
    }
    return rtlfseek(f, offset, origin) == 0 ? stderror(.ok) : stderror(.eof)
}

fn rtlftell(f: File): int

fn ftell*(f: File): (int, Err) {
    if f == null {
        return 0, stderror(.nullf)
    }
    pos := rtlftell(f)
    return pos, pos >= 0 ? stderror(.ok) : stderror(.eof)
}

fn rtlremove(name: str): int

fn remove*(name: str): Err {
    return rtlremove(name) == 0 ? stderror(.ok) : stderror(.eof)
}

fn rtlfeof(f: File): int

fn feof*(f: File): bool {
    if f == null {
        return true
    }
    eof := bool(rtlfeof(f))               
    return eof
}

fn rtlfflush(f: File): int

fn fflush*(f: File): Err {
	if f == null {
		return stderror(.nullf)
	}
	return rtlfflush(f) == 0 ? stderror(.ok) : stderror(.eof)
}

fn rtlfread (buf: ^void, size, cnt: int, f: File): int
fn rtlfwrite(buf: ^void, size, cnt: int, f: File): int

fn freadwrite(f: File, buf: any, reading: bool): (int, Err) {
    if f == null {
        return 0, stderror(.nullf)
    }
    var data: ^void
    var length: int 
    switch bytes := type(buf) {
        case ^[]int8:  length = len(bytes^); if length > 0 {data = &bytes[0]} 
        case ^[]uint8: length = len(bytes^); if length > 0 {data = &bytes[0]}
        case ^[]char:  length = len(bytes^); if length > 0 {data = &bytes[0]}
        default:
            if selfhasptr(buf) {
                return 0, stderror(.ptr)
            }
            length, data = sizeofself(buf), buf.__self
    }
    if length == 0 || data == null {
        return 0, stderror(.ok)
    }
    size := reading ? rtlfread(data, 1, length, f) : rtlfwrite(data, 1, length, f)
    err := size == length ? stderror(.ok) : stderror(.eof)
    return size, err
}

fn fread*(f: File, buf: any): (int, Err) {
    return freadwrite(f, buf, true)
}

fn freadall*(f: File): ([]char, Err) {
    err := fseek(f, 0, .end)
    if err.code != 0 {
        return {}, err
    }
    size, err := ftell(f)
    if err.code != 0 {
        return {}, err
    }
    err = fseek(f, 0, .begin)
    if err.code != 0 {
        return {}, err
    }
    data := make([]char, size)
    _, err := fread(f, &data)
    if err.code != 0 {
        return data, err
    }
    return data, stderror(.ok)
}

fn fwrite*(f: File, buf: any): (int, Err) {
    return freadwrite(f, buf, false)
}

fn println*(s: str): int {
    return printf("%s\n", s)
}

fn fprintln*(f: File, s: str): int {
    return fprintf(f, "%s\n", s)
}

fn getchar*(): char {
    var c: char
    scanf("%c", &c)
    return c
}

// Strings

fn ltrim*(s: str): str {
    start := -1
    slen := len(s)
    for i := 0; i < slen; i++ {
        if s[i] > ' ' {
            start = i
            break
        } 
    }
    if start == -1 {return ""}
    return slice(s, start)
}

fn rtrim*(s: str): str {
    stop := -1
    slen := len(s)
    for i := slen - 1; i >= 0; i-- {
        if s[i] > ' ' {
            stop = i
            break
        } 
    }
    if stop == -1 {return ""}
    return slice(s, 0, stop + 1)
}

fn trim*(s: str): str {
    return ltrim(rtrim(s))
}

// Conversions

fn atoi*(s: str): int {
    var x: int
    sscanf(s, "%lld", &x)
    return x
}

fn atof*(s: str): real {
    var x: real
    sscanf(s, "%lf", &x)
    return x
}

fn itoa*(x: int): str {
    s := sprintf("%lld", x)
    return s
}

fn ftoa*(x: real, decimals: int): str {
    fmt := sprintf("%%.%lldlf", decimals)
    s := sprintf(fmt, x)
    return s
}

// Math

const pi* = 3.14159265358979323846

// Random numbers

const randMax* = 0x7FFFFFFF

randSeed := uint(1)

fn rand*(): int {
    // xorshift
    randSeed ~= randSeed << 13
    randSeed ~= randSeed >> 7
    randSeed ~= randSeed << 17
    return randSeed & randMax
}

fn frand*(): real {
    return real(rand()) / randMax
}

fn srand*(seed: int) {
    if seed > 0 {
        randSeed = seed
        rand()
    }
}

// Time

type DateTime* = struct {
    second, minute, hour: int
    day, month, year: int
    dayOfWeek, dayOfYear: int
    isDST: bool
}

fn rtltime(): int

fn time*(): int {
    return rtltime()
}

fn rtlclock(): real

fn clock*(): real {
    return rtlclock()
}

fn rtllocaltime(t: int): DateTime

fn localtime*(t: int): DateTime {
    return rtllocaltime(t)
}

fn rtlgmtime(t: int): DateTime

fn gmtime*(t: int): DateTime {
    return rtlgmtime(t)
}

fn rtlmktime(d: ^DateTime): int

fn mktime*(d: DateTime): int {
    return rtlmktime(&d)
}

fn timestr*(d: DateTime): str {
    return sprintf("%4d-%02d-%02d %02d:%02d:%02d", 
                   d.year, d.month, d.day,
                   d.hour, d.minute, d.second)
}

// Command line and environment

fn argc*(): int {
    return len(rtlargv)
}

fn argv*(i: int): str {
    return rtlargv[i]
}

fn rtlgetenv(name: str): str

fn getenv*(name: str): str {
    return rtlgetenv(name)
}

fn rtlsystem(command: str): int

fn system*(command: str): int {
    return rtlsystem(command)
}
