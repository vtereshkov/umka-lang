// UTF-8 utilities

type (
    Rune = int32                                
)

fn (r: ^Rune) size*(): int {
    if r^ <= 0x7f {
        return 1
    }

    if r^ <= 0x7ff {
        return 2
    }

    if r^ <= 0xffff {
        return 3
    }

    if r^ <= 0x10ffff {
        return 4
    }

    return 0
}

fn (r: ^Rune) encode*(): str {
    s := r.size()

    if s == 1 {
        return str(char(r^))
    }

    out := make([]char, s + 1)

    // header is size amount of ones at the start
    header := 0
    for i := 0; i < s; i++ {
        header |= 1 << (7 - i)
    }

    shift := (s - 1) * 6
    out[0] = char(((r^ >> shift & 0xff) | header) & 0xff)

    for i := 1; i < s; i++ {
        shift -= 6
        out[i] = char(r^ >> shift & 0x3f | 0x80)
    }

    return str(out)
}

fn getNextRune*(a: str, pos: int): Rune {
    c := int32(a[pos])

    // exit early if ascii character is detected
    if c & 0x80 == 0 {
        return Rune(c)
    }

    // find size of the rune
    size := 1
    for pos + size < len(a) && int(a[pos + size]) & 0xc0 == 0x80 { 
        size++
    }

    // generate mask for the first byte
    mask := 0
    for i := 0; i < 8 - size - 1; i++ {
        mask |= 1 << i
    }

    sum := 0
    shift := (size - 1) * 6
    for i := 0; i < size && pos + i < len(a); i++ {
        sum |= ((int32(a[pos + i]) & mask) << shift)
        if i == 0 {
            mask = 0x3f // following bytes have 0011 1111 mask
        }
        shift -= 6
    }

    return sum
}

fn decode*(s: str): []Rune {
    runes := make([]Rune, len(s))
    runeCount := 0

    l := len(s)
    for i := 0; i < l; runeCount++ {
        runes[runeCount] = getNextRune(s, i)
        i += runes[runeCount].size()
    }

    return slice(runes, 0, runeCount)
}

fn realLength*(s: str): int {
    rl := 0
    l := len(s)
    for i := 0; i < l; rl++ {
        r := getNextRune(s, i)
        i += r.size()
    }

    return rl
}
